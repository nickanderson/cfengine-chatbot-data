:properties:
:CFEngine_Example_Index: [[id:38277465-771a-4db4-983a-8dfd434b1aff][CFEngine_examples]]
:CFEngine_Functions: [[id:a450a1fd-d361-4d1e-85e3-7c1954313683][Function: data_expand()]] [[id:7b2ad788-6a8d-4f1a-94e5-6947fd7e2431][Function: string_mustache()]] [[id:cb299172-277a-42de-a1c9-c82e54379e4e][Function: getindices()]] [[id:176a208f-384f-4543-aaf4-599d03cbfa5d][Function: mergedata()]] [[id:e95dec7a-9b23-4e95-b47d-c8da319071eb][Function: classfiltercsv()]] [[id:763031aa-965b-4c99-a5e1-544efa40d7bf][Function: lsdir()]]
:CFEngine_PromiseTypes: [[id:b31e06a4-d3b1-44f2-9292-cd20ca17cb66][Promise type: vars]] [[id:c458bf16-1ba9-499f-a801-e94e0f80a5c9][Promise type: reports]]
:ID:       fd6a4691-1752-4a02-ba90-30867de7a200
:end:
#+title: Example illustrating loading data from many class filtered csv files with the result merged into a single data structure

#+begin_src cfengine3 :tangle loading_data_from_many_class_filtered_csv_files_with_the_result_merged_into_a_single_data_structure.cf
  bundle agent __main__
  {
    %?
  }
#+end_src

#+CAPTION: csv/filemode.csv
#+BEGIN_SRC csv :tangle ~/vagrant/CFEngine/3.12.1/CFE-2768/csv/filemode.csv
  classexpression,file,permission
  any,/tmp/file1,600
#+END_SRC

#+CAPTION: csv/filemode.d/000.csv
#+BEGIN_SRC csv :tangle ~/vagrant/CFEngine/3.12.1/CFE-2768/csv/filemode.d/000.csv
  classexpression,file,permission
  any,/tmp/file2,700
#+END_SRC

#+CAPTION: csv/filemode.d/001.csv
#+BEGIN_SRC csv :tangle ~/vagrant/CFEngine/3.12.1/CFE-2768/csv/filemode.d/001.csv
  classexpression,file,permission
  any,$(main.MYFILE),777
#+END_SRC

The CSV files *need* to have CRLF line endings.

#+BEGIN_SRC sh :dir ~/vagrant/CFEngine/3.12.1/CFE-2768
  find . -name "*.csv" | xargs -n 1 unix2dos
#+END_SRC


#+CAPTION: test.cf
#+BEGIN_SRC cfengine3 :tangle ~/vagrant/CFEngine/3.12.1/CFE-2768/test.cf :include-stdlib f :verbose-mode nil :inform-mode nil
      bundle agent main
      #@brief Example loading data from many class filtered csv files merged into a single data structure
      {
        vars:
            "MYFILE" string => "/tmp/NicksFile";

            "item"          string => "filemode";
            "csvdir"        string => "$(this.promise_dirname)/csv";

            "csvfiles"
              slist  => { "$(csvdir)/$(item).csv",
                          lsdir("$(csvdir)/$(item).d", ".*.csv", "true") };

            "csvdata" data   => '[]';
            "csvdata"
              data => mergedata("csvdata",
                                classfiltercsv( $(csvfiles),
                                                "true", # Data file contains column headings
                                                0));    # Column containing class expression to filter with

            "i" slist => getindices(csvdata);

            # Enforce that each file in the merged, filtered data set has permissions enforced
            # files:
            #   "$(csvdata[$(i)][file])"
            #     perms => m( "$(csvdata[$(i)][permission])" );

        reports:
            "$(csvfiles)" printfile => cat( $(csvfiles) );

            "Expanded$(const.n)$(with)"
              with => string_mustache( "{{%-top-}}", data_expand(csvdata));
      }

      body perms m(mode)
      # @brief Set the file mode
      # @param mode The new mode
      {
              mode   => "$(mode)";
      }
      body printfile cat(file)
      # @brief Report the contents of a file
      # @param file The full path of the file to report
      {
              file_to_print => "$(file)";
              number_of_lines => "inf";
      }
#+END_SRC

#+BEGIN_SRC sh :dir ~/vagrant/CFEngine/3.12.1/CFE-2768
  chmod 600 test.cf
  cf-agent -KIf ./test.cf --show-evaluated-vars=default:main.csvdata
#+END_SRC

#+BEGIN_EXAMPLE
  [root@host001 CFE-2768]#   cf-agent -KIf ./test.cf --show-evaluated-vars=default:main.csvdata
  R: /vagrant/CFE-2768/./csv/filemode.csv
  R: classexpression,file,permission
  R: any,/tmp/file1,600
  R: /vagrant/CFE-2768/./csv/filemode.d/000.csv
  R: classexpression,file,permission
  R: any,/tmp/file2,700
  R: /vagrant/CFE-2768/./csv/filemode.d/001.csv
  R: classexpression,file,permission
  R: any,$(main.MYFILE),777
  R: Expanded
  [
    {
      "file": "/tmp/file1",
      "permission": "600"
    },
    {
      "file": "/tmp/file2",
      "permission": "700"
    },
    {
      "file": "/tmp/NicksFile",
      "permission": "777"
    }
  ]
  Variable name                            Variable value                                               Meta tags
  default:main.csvdata                     [{"file":"/tmp/file1","permission":"600"},{"file":"/tmp/file2","permission":"700"},{"file":"$(main.MYFILE)","permission":"777"}] source=promise
#+END_EXAMPLE
