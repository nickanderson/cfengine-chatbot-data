:PROPERTIES:
:ID:       46efe84b-46f3-444b-8d36-80090ac755fa
:CREATED:  [2021-09-17 Fri 11:22]
:END:
#+title: Example illustrating one-shot-promise

Do something once, and /never again/.

CFEngine is not designed to facilitate this sort of pattern. It's quite doable, but not very succinct.

#+caption: Example Policy
#+begin_src cfengine3 :include-stdlib t :log-level info :exports both :wrap example
    bundle agent __main__
    {
      commands:
          "/bin/echo 'Never more, quoth the raven'"
            classes => results( "bundle", "one_shot_echo" ),
            unless => fileexists( "$(sys.statedir)/one-shot/one_shot_echo_reached" );
    
      files:
          "$(sys.statedir)/one-shot/one_shot_echo_reached"
            create => "true",
            if => "one_shot_echo_reached"; # Perhaps one_shot_echo_repaired if you
                                           # care that it was successful and not
                                           # just attempted.
      reports:
        "$(sys.statedir)/one-shot/one_shot_echo_reached Missing"
            unless => fileexists( "$(sys.statedir)/one-shot/one_shot_echo_reached" );
        "$(sys.statedir)/one-shot/one_shot_echo_reached Exists"
            if => fileexists( "$(sys.statedir)/one-shot/one_shot_echo_reached" );
    }
#+end_src

First Execution:

#+begin_example
    info: Executing 'no timeout' ... '/bin/echo 'Never more, quoth the raven''
  notice: Q: ".../bin/echo 'Neve": Never more, quoth the raven
    info: Last 1 quoted lines were generated by promiser '/bin/echo 'Never more, quoth the raven''
    info: Completed execution of '/bin/echo 'Never more, quoth the raven''
R: /home/nickanderson/.cfagent/state/one-shot/one_shot_echo_reached Missing
    info: Created directory for '/home/nickanderson/.cfagent/state/one-shot/one_shot_echo_reached'
    info: Created file '/home/nickanderson/.cfagent/state/one-shot/one_shot_echo_reached', mode 0600
R: /home/nickanderson/.cfagent/state/one-shot/one_shot_echo_reached Exists
#+end_example

Subsequent executions:

#+begin_example
R: /home/nickanderson/.cfagent/state/one-shot/one_shot_echo_reached Exists
#+end_example

* Thoughts on something more dynamic

Leverage metadata (classes/variables)matching to find the kind of signal. But the value is date based.

Hit button on app for one time power off.
App makes api call to Enterprise Hub, setting the host specific var power_off_DATESTAMP, e.g. power_off_2021_09_17_01_01_01".

#+begin_src cfengine3 :include-stdlib t :log-level info :exports both :wrap example
    bundle agent __main__
    {
      vars:
          "echo_1631897060"
            string => "Never more, quote the raven",
            meta => { "echo_signal" };
    
          "echo_signals" data => variablesmatching_as_data( ".*", echo_signal );
          "_echo_signals_idx" slist => getindices( echo_signals );
    
      commands:
          "/bin/echo $(echo_signals[$(_echo_signals_idx)])"
            unless => fileexists( "$(sys.statedir)/one-shot/$(_echo_signals_idx)" ),
            classes => results( "bundle", "$(_echo_signals_idx)" );
    
      files:
          "$(sys.statedir)/one-shot/." create => "true";
          "$(sys.statedir)/one-shot/$(_echo_signals_idx)"
            content => "/bin/echo $(echo_signals[$(_echo_signals_idx)])",
            if => canonify( "$(_echo_signals_idx)_repaired" );
    
      reports:
          "echo execution for '$(echo_signals[$(_echo_signals_idx)])' since '$(sys.statedir)/one-shot/$(_echo_signals_idx)' is absent"
            unless => fileexists( "$(sys.statedir)/one-shot/$(_echo_signals_idx)" );
    
          "Skipping echo execution for '$(echo_signals[$(_echo_signals_idx)])' since '$(sys.statedir)/one-shot/$(_echo_signals_idx)' exists"
            unless => or( not( fileexists( "$(sys.statedir)/one-shot/$(_echo_signals_idx)") ),
                          canonify( "$(_echo_signals_idx)_repaired" ));
     }
#+end_src

#+RESULTS:
#+begin_example
    info: Created directory '/home/nickanderson/.cfagent/state/one-shot/.'
    info: Executing 'no timeout' ... '/bin/echo Never more, quote the raven'
  notice: Q: ".../bin/echo Never": Never more, quote the raven
    info: Last 1 quoted lines were generated by promiser '/bin/echo Never more, quote the raven'
    info: Completed execution of '/bin/echo Never more, quote the raven'
R: echo execution for 'Never more, quote the raven' since '/home/nickanderson/.cfagent/state/one-shot/default:main.echo_1631897060' is absent
    info: Updated content of '/home/nickanderson/.cfagent/state/one-shot/default:main.echo_1631897060' with content '/bin/echo Never more, quote the raven'
#+end_example

First execution:

Subsequent executions:


* References
- [[id:38277465-771a-4db4-983a-8dfd434b1aff][CFEngine Examples]]
