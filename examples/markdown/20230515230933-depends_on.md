``` {.cfengine3 exports="both" wrap="EXAMPLE" tangle="depends_on.cf"}
bundle agent main
{
  methods:
    "never" depends_on => { "never" };
    "zero"
      depends_on => { "one", "two" };
    "one" handle => "one";
    "two" handle => "two";
}

bundle agent one
{
   reports:
     "In $(this.bundle) I depend on nothing"
       handle => "report_1_bundle_one";

     "In $(this.bundle) I depend on report_1_bundle_two"
       depends_on => { "report_1_bundle_two" };
}

bundle agent two
{
   reports:
     "In $(this.bundle) I depend on nothing"
       handle => "report_1_bundle_two";

     "In $(this.bundle) I depend on report_1_bundle_one"
       depends_on => { "report_1_bundle_one" };
}

bundle agent zero
{
  reports:
    "in bundle $(this.bundle) This promise doenst depend on anything, but the bundle actuation itself might.";
}

bundle agent never
{
  reports:
    "in bundle $(this.bundle) This promise doenst depend on anything, but the bundle actuation itself might.";
}
```

``` example
R: In one I depend on nothing
R: In two I depend on nothing
R: In two I depend on report_1_bundle_one
R: in bundle zero This promise doenst depend on anything, but the bundle actuation itself might.
```
