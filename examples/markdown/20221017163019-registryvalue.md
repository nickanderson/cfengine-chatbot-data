Querying a registry value that does not exist results in an error. Avoid
the errors by only querying for registry values that you know exist. For
example condition the promise on the result of reg query lookup for the
key.

``` {.cfengine3 exports="both" wrap="EXAMPLE" tangle="registryvalue.cf"}
bundle agent wn12_so_000071
{
  vars:
    windows::
      # reg query $(HIVE)\System\CurrentControlSet\Control\Lsa /v LimitBlankPasswordUse
      "HIVE" string => "HKEY_LOCAL_MACHINE";
      "PATH" string => "\Software\Microsoft\Windows NT\CurrentVersion\Setup\RecoveryConsole";
      "KEY" string => "SecurityLevel";
      "finding_id" string => "WN12-SO-000071";

      "rv"
        string => registryvalue( "$(HIVE)$(PATH)",
                                 "$(KEY)" ),
        if => returnszero( "reg query $(HIVE)$(PATH) /v $(KEY) > NUL: 2>&1", useshell);

      # If the registry value does not exist or is not configured as specified, this is a finding:

      "finding"
        string => "$(finding_id)",
        if => not( isvariable( "rv" ) ),
        meta => { "inventory", "attribute_name=Microsoft Windows Server 2012 Member Server Security Technical Implementation Guide finding" },
        comment => "Registry value does not exist";

      "finding"
        string => "$(finding_id)",
        if => not( strcmp( "$(rv)", "1" ) ),
        meta => { "inventory", "attribute_name=Microsoft Windows Server 2012 Member Server Security Technical Implementation Guide finding" },
        comment => "Value not configured as specified";
}

bundle agent __main__
{
  methods:
      "wn12_so_000071";
}
```

**Notes:** Currently (as of 3.18) values of type `REG_SZ`{.verbatim}
(string), `REG_EXPAND_SZ`{.verbatim} (expandable string) and
`REG_DWORD`{.verbatim} (double word) are supported.
