The grub config can be challenging to line edit. It\'s got a mix of
plain value and key/value items.

# Parsing values and munging {#40a648ab-d7f5-4e9c-b8a0-326309ac7f17}

One path would be to parse out the current settings from the file,
munged those, then promise the munged line.

:::: captioned-content
::: caption
Example extracting current config from file and munging
:::

``` {.cfengine3 include-stdlib="t" log-level="info" exports="both" extra-opts="--show-evaluated-vars=default:main\\."}
bundle agent main
{
   vars:
      # Get on disk config
      "c" data => readenvfile( "/etc/default/grub"  );

      # Get list of GRUB_CMDLINE_LINUX_DEFAULT values
      "d"
        slist => string_split( "$(c[GRUB_CMDLINE_LINUX_DEFAULT])", "\s+", inf ),
        meta => { "inventory", "attribute_name=/etc/default/grub GRUB_CMDLINE_LINUX_DEFAULT"};

      # Regular expressions matching things we don't want in the line
      "regexes_shun" slist => { "mdns=off", "tsx.*", "quiet", ".*spectre.*" };

      # Entries for things we want in the line (applied /after/ shunning the ones we don't want)
      "wants" slist => { "mdns=on", "nospectre_v1" };

      # Generate a regular expression to match all of the values in the config line you want to remove.
      "shunned" slist => filter( concat( '(', join( "|", @(regexes_shun) ), ')' ), # Regex to match unwanted entries
                                 d,     # List to match against
                                 yes,   # Use regex
                                 yes,   # invert result (return unmatched entries)
                                 inf ); # No limits

      # Get a list of the values after removing ones we don't want
      "final" slist => { @(shunned), @(wants) };

      # Sort the list to avoid case of potential change during each agent execution.
      "final_sorted" slist => sort( @(final), "lex" );

      # From here you can use edit_line with a files promise to set
      # =GRUB_CMDLINE_LINUX_DEFAULT= Perhaps promises can be made against a
      # staging file that could be validated with some tooling similar to how
      # you can use ~visudo -c~, ~apachectl -t~, ~sshd -T~, or ~sshd -t~ to
      # check the validity of a sudoers, httpd, or sshd config file to validate
      # the promised staged file is valid before making a promise about the
      # final destination.

}
```
::::

``` example
Variable name                            Variable value                                               Meta tags                                Comment
default:main.c                           {"GRUB_CMDLINE_LINUX":"","GRUB_CMDLINE_LINUX_DEFAULT":"quiet splash noibrs noibpb nopti nospectre_v2 nospectre_v1 l1tf=off nospec_store_bypass_disable no_stf_barrier mds=off tsx=on tsx_async_abort=off mitigations=off","GRUB_DEFAULT":"0","GRUB_DISTRIBUTOR":"`lsb_release -i -s 2> /dev/null || echo Debian`","GRUB_TIMEOUT":"0"} source=promise
default:main.d                            {"quiet","splash","noibrs","noibpb","nopti","nospectre_v2","nospectre_v1","l1tf=off","nospec_store_bypass_disable","no_stf_barrier","mds=off","tsx=on","tsx_async_abort=off","mitigations=off"} source=promise
default:main.final                        {"splash","noibrs","noibpb","nopti","l1tf=off","nospec_store_bypass_disable","no_stf_barrier","mds=off","mitigations=off","mdns=on"} source=promise
default:main.final_sorted                 {"l1tf=off","mdns=on","mds=off","mitigations=off","no_stf_barrier","noibpb","noibrs","nopti","nospec_store_bypass_disable","splash"} source=promise
default:main.regexes_shun                 {"mdns=off","tsx.*","quiet",".*spectre.*"}                  source=promise
default:main.shunned                      {"splash","noibrs","noibpb","nopti","l1tf=off","nospec_store_bypass_disable","no_stf_barrier","mds=off","mitigations=off"} source=promise
default:main.wants                        {"mdns=on"}                                                 source=promise
default:maintain_key_values_meta.tags     {"deprecated=3.6.0","deprecation-reason=Generic reimplementation","replaced-by=set_line_based"} source=promise
```

# Using field~edits~ {#using-fieldedits created="<2024-07-03 Wed 16:12>"}

Perhaps more clean would be using field edits.

``` {.cfengine3 log-level="info"}
bundle agent __main__
{
      files:

      # Initialize the file with mock data
      # "/tmp/grub.conf"
        #   content => 'GRUB_CMDLINE_LINUX="MARK rhgb quiet"';

        # "/tmp/grub.conf"
        #   content => 'GRUB_CMDLINE_LINUX="rhgb MARK quiet"';

        "/tmp/grub.conf"
          content => 'GRUB_CMDLINE_LINUX="rhgb quiet"';

      # NAMING THINGS IS HARD!
        "/tmp/grub.conf"
          edit_line => key_equals_dblquoted_value_contains_space_separated_subvalue("GRUB_CMDLINE_LINUX", "MARK");

      reports:
      "/tmp/grub.conf" printfile => cat( "$(this.promiser)" );
}
bundle edit_line key_equals_dblquoted_value_contains_space_separated_subvalue(key, subvalue)
{
  field_edits:
      '$(key)=.*'
        edit_field => dblquoted_value_space_separated_subvalue($(subvalue));
}
body edit_field dblquoted_value_space_separated_subvalue(subvalue)
{
  start_fields_from_zero => "true";
  allow_blank_fields => "true"; # This is necessary or the closing field separator won't be inserted. (leaving a blank field)
  select_field => "1";
  extend_fields => "true";
  field_separator=>'"'; # Double quotes are the field seperator
  field_value => '$(subvalue)';
  value_separator => " ";
  field_operation => "append";
}
```
