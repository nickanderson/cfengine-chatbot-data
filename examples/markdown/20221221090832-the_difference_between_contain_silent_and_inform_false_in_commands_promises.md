`inform => "false"` suppresses the info level logs from commands
promises. In the example below three commands are run. One suppressing
nothing, one suppressing command *output* and one suppressing both
command output and *inform* log messages.

``` {.cfengine3 include-stdlib="t" log-level="info" exports="both" command-in-result="t" tangle="the_difference_between_contain_silent_and_inform_false_in_commands_promises.cf"}
bundle agent __main__
{
  commands:
      "echo"
        arglist => { "Hello", "with", "inform true",
                     "without silence" },
        inform => "true",
        contain => in_shell,
        handle => "i_suppress_nothing";

      "echo"
        arglist => { "Hello", "with", "inform true",
                     "with contain in_shell_and_silent" },
        inform => "true",
        contain => in_shell_and_silent,
        handle => "i_suppress_command_output";

      # NOTE: You won't see this command execution when running cf-agent with info
      # level logging (--log-level info, --inform, -I)

      "echo"
        arglist => { "Hello", "with", 'inform false',
                     "with contain in_shell_and_silent" },
        inform => "false",
        contain => in_shell_and_silent,
        handle => "i_suppress_command_output_and_inform_output";
}
```

``` example
# cf-agent --no-lock --log-level info --file the_difference_between_contain_silent_and_inform_false_in_commands_promises.cf
    info: Executing 'no timeout' ... 'echo Hello with inform true without silence'
  notice: Q: "...echo Hello with": Hello with inform true without silence
    info: Last 1 quoted lines were generated by promiser 'echo Hello with inform true without silence'
    info: Completed execution of 'echo Hello with inform true without silence'
    info: Executing 'no timeout' ... 'echo Hello with inform true with contain in_shell_and_silent'
    info: Completed execution of 'echo Hello with inform true with contain in_shell_and_silent'
```
