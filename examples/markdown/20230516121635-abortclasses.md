``` {.cfengine3 include-stdlib="no" exports="both" tangle="abortclasses.cf"}
body agent control
{
      # Abort agent run if any class starting with eject is defined.
        abortclasses => { "eject.*" };
}

bundle agent main
{
  methods:
      "ready";
      "set";
      "go";
}

bundle agent ready
{
  reports:
      "I am $(this.bundle)";
}
bundle agent set
{
  classes:
      "eject_me" expression => "any";

  reports:
      "I am $(this.bundle)";
}
bundle agent go
{
  reports:
      "I am $(this.bundle)";
}
```

``` example
R: I am ready
   error: Fatal CFEngine error: cf-agent aborted on defined class 'eject_me'
```
